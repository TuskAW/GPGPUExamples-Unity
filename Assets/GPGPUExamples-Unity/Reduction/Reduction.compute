#pragma kernel CsSetData
#pragma kernel CsReduction
#pragma kernel CsCopyBuffer

#define THREADS_PER_GROUP 512

RWStructuredBuffer<float> _DataArray;
RWStructuredBuffer<float> _PartialSumWrite;
StructuredBuffer<float> _PartialSumRead;

groupshared float groupsum[THREADS_PER_GROUP];

cbuffer params
{
    int _PartialSumSize;
};

[numthreads(THREADS_PER_GROUP, 1, 1)]
void CsSetData(uint id : SV_DispatchThreadID)
{
    // Data length
    _DataArray[id] = 1.0;

    // Leibniz formula for π
    // uint k = id;
    // float sign = (k % 2 == 0) ? 1.0 : -1.0;
    // _DataArray[k] = sign/(2.0*k + 1.0);
}

[numthreads(THREADS_PER_GROUP, 1, 1)]
void CsReduction(uint id : SV_DispatchThreadID, uint gtid : SV_GroupThreadID, uint gid : SV_GroupID)
{
    // Each thread loads one element from global to shared mem
    groupsum[gtid] = _DataArray[id];
    GroupMemoryBarrierWithGroupSync();

    // Do reduction in shared mem
    for(unsigned int s=THREADS_PER_GROUP/2; s>0; s>>=1)
    {
        if (gtid < s)
        {
            groupsum[gtid] += groupsum[gtid + s];
        }
        GroupMemoryBarrierWithGroupSync();
    }

    // Write result for this block to global mem
    if (gtid == 0)
    {
        _PartialSumWrite[gid] = groupsum[0];
    }
}

[numthreads(THREADS_PER_GROUP, 1, 1)]
void CsCopyBuffer(uint id : SV_DispatchThreadID, uint gtid : SV_GroupThreadID, uint gid : SV_GroupID)
{
    if (id < _PartialSumSize)
    {
        _PartialSumWrite[id] = _PartialSumRead[id];
    }
    else
    {
        _PartialSumWrite[id] = 0.0;
    }
}
